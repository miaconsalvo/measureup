title: HeatherInterviewStart
tags: 
position: -574,40
---
<<Contestant Heather base.neutral center lower 0>>
<<FadeIn Heather 0.5>>
<<wait 0.5>>

Heather: Oh hi! You must be the stylist, nice to meet you I'm Heather.
    -> Hi, nice to meet you!
    -> How are you doing?
        Heather: A bit nervous, to be honest, but also excited.
Heather: Jo told me you were going to ask me some questions before we dive into the makeover.
Heather: Should we just dive right in?
<<jump HeatherInterviewQuestions>>
===
title: HeatherInterviewQuestions
tags: 
position: -376,44
---
<< if max_questions() == true>>
    <<jump HeatherInterviewEnd>>
<<endif>>

-> A. How would you describe your day-to-day wardrobe? <<if question_asked(0) == false>>
    <<jump HeatherInterviewQuestionA>>
-> B. What would you never be caught dead wearing? <<if question_asked(1) == false>>
    <<jump HeatherInterviewQuestionB>>
-> C. What is your most loved item in your wardrobe? <<if question_asked(2) == false>>
    <<jump HeatherInterviewQuestionC>>
-> D. What do you want to get out of this makeover? <<if question_asked(3) == false>>
    <<jump HeatherInterviewQuestionD>>
-> E. What is an outfit you've worn that you'll never wear again? <<if question_asked(4) == false>>
    <<jump HeatherInterviewQuestionE>>
===
title: HeatherInterviewQuestionA
tags: 
position: -192,-140
---
Heather: Well, with the kids at school and in after school sports and activities I usually go with comfort over anything else. 
Heather: Yoga pants are my go to. Usually a [tag]T-Shirt[/tag] with it, and then sneakers. 
Heather: It's a pretty basic vibe.
<<add_note 0 0>>
<<jump HeatherInterviewQuestions>>
===
title: HeatherInterviewQuestionB
tags: 
position: 16,-138
---
Heather: That's easy. Skirts.
Heather: I hated when wardrobe would put the big frilly and flashy ones on me as Jenny on The Family Tree.
Heather: I think that has stuck with me ever since. 
Heather: It made sense for the character, I guess, but as a kid I haaaated them.
<<add_note 1 1>>
Heather: Especially the really brightly coloured ones.
<<jump HeatherInterviewQuestions>>
===
title: HeatherInterviewQuestionC
tags: 
position: 230,-131
---
Heather: Hmm... like worn the most? Or what I love for its style the most?
-> What you wear the the most.
    Heather: That would be my tried and true black yoga pants.
    Heather: I think I own like 5 pairs of the same ones. 
    Heather: Actually. I almost bought some patterned ones a few weeks ago, but I don't know if I can pull them off.
    <<add_note 2 2>>
-> What you love the most, regardless of how often you wear it. 
    Heather:  I have a designer letterman-style jacket that I'll wear to the kids games that they got me (well their dad did, but you know how it goes!). 
    Heather: I love that it's practical but I still feel stylish in it - the sleeves have this cool floral pattern on it.
    Heather: It isn't something I would usually choose for myself so it was a great gift.
    <<add_note 2 3>>
<<jump HeatherInterviewQuestions>>
===
title: HeatherInterviewQuestionD
tags: 
position: -119,103
---
Heather: I'd like to feel like I have an outfit or style beyond the school run mom.
Heather: As much as I live and die by my athleisure wardrobe, I know I can pull off some other looks - I just need a helping hand.
<<add_note 3 4>>
<<jump HeatherInterviewQuestions>>
===
title: HeatherInterviewQuestionE
tags: 
position: 113,100
---
Heather: I had to wear a blazer and suit pants to court, thanks to some actions of an old co-star who needed a character witness for a custody case.
Heather: That day cost him two favours because I despise uncomfy, stuffy clothes, and dealing with lawyers, haha!
<<add_note 4 5>>
<<jump HeatherInterviewQuestions>>
===
title: HeatherInterviewEnd
tags: 
position: 497,5
---
<<FadeOut Heather 0.5>>
<<wait 0.5>>
<<HideAll>>
===
